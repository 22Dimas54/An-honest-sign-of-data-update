
#Область ОписаниеПеременных

Перем URL;
Перем Адрес;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция возвращает структуру с регистрационными данными внешней обработки,
// включая наименование, версию, информацию и команды.
// 
// Возвращаемое значение:
// Структура - Регистрационные данные внешней обработки
Функция СведенияОВнешнейОбработке() Экспорт
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Наименование", "Честный знак обновление данных");
	РегистрационныеДанные.Вставить("ФормированиеФоновогоЗадания", Истина);
	РегистрационныеДанные.Вставить("ВерсияБСП", "1.2.4.1");
	РегистрационныеДанные.Вставить("Версия", "1.0");
	РегистрационныеДанные.Вставить("Информация", "Честный знак обновление данных");
	
	ТЗКоманд = Новый ТаблицаЗначений;
	ТЗКоманд.Колонки.Добавить("Идентификатор");
	ТЗКоманд.Колонки.Добавить("Представление");
	
	СтрокаКоманды = тзКоманд.Добавить();
	СтрокаКоманды.Идентификатор = Новый УникальныйИдентификатор;
	СтрокаКоманды.Представление = "Честный знак обновление данных";
	
	РегистрационныеДанные.Вставить("Команды", ТЗКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

// Выполняет команду, идентифицируемую уникальным идентификатором, 
// с использованием предоставленных параметров обработки.
// В данном случае вызывает процедуру выполнения задания для выгрузки QR-кодов подлинности.
// 
// Параметры:
//  ИдентификаторКоманды - УникальныйИдентификатор - Идентификатор команды, которую необходимо выполнить.
//  ПараметрыОбработки   - Структура               - Параметры, используемые для обработки команды.
Процедура ВыполнитьКоманду(ИдентификаторКоманды,ПараметрыОбработки) Экспорт
	
	ВыполнитьЗадание();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет задание по выгрузке данных на внешний сервер. Включает подготовку данных,
// установку соединения, выгрузку штрихкодов и закрытие соединения.
Процедура ВыполнитьЗадание() Экспорт
	
	Ответ = ЗапросДанных();
	Результат = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	Данные = ОбщегоНазначения.JSONВЗначение(Результат, , Ложь);
	
	Если Не Данные.Свойство("result") Тогда
		ВызватьИсключение СтрШаблон("Отсутствует поле. %1 в данных", "result");
		Возврат;
	КонецЕсли;
	
	Если Не Данные.result.Свойство("goods") Тогда
		ВызватьИсключение СтрШаблон("Отсутствует поле. %1 в данных", "goods");
	КонецЕсли;
	
	Штрихкоды = Штрихкоды(Данные.result.goods);
	Если Штрихкоды.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроставитьЧестныйЗнакНоменклатуре(Штрихкоды);
	
КонецПроцедуры

Функция ЗапросДанных()
	
	Соединение  = ПолучитьСоединение();
	
	Заголовки   = Новый Соответствие;
	Запрос = Новый HTTPЗапрос(Адрес, Заголовки);
	
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ОтветСтрокой = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		ВызватьИсключение СтрШаблон("Ошибка отправки вложения в телеграм.
		|Код состояния: %1
		|Тело: %2"
		, Ответ.КодСостояния
		, ОтветСтрокой
		);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСоединение()
	
	SSL = Новый ЗащищенноеСоединениеOpenSSL();
	Соединение = Новый HTTPСоединение(URL, 443, , , , 30, SSL, Ложь);
	Возврат Соединение;
	
КонецФункции

Функция Штрихкоды(Данные)
	
	Штрихкоды = Новый Массив;
	
	Для Каждого Элемент Из Данные Цикл
		Если Не Элемент.Свойство("gtin") Тогда
			Продолжить;
		КонецЕсли;
		Штрихкоды.Добавить(Элемент["gtin"]);
	КонецЦикла;
	
	Возврат Штрихкоды;
	
КонецФункции

Функция ТекстЗапросаНоменклатура()
	
	ТекстЗапросаНоменклатура = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Штрихкоды.Владелец КАК Номенклатура,
		|	Штрихкоды.Владелец.Код КАК Код,
		|	Штрихкоды.Владелец.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.Штрихкоды КАК Штрихкоды
		|ГДЕ
		|	Штрихкоды.Штрихкод В(&Штрихкод)
		|	И ВЫРАЗИТЬ(Штрихкоды.Владелец КАК Справочник.Номенклатура).ЧестныйЗнак = ЛОЖЬ";
	
	Возврат ТекстЗапросаНоменклатура;
	
КонецФункции

Процедура ПроставитьЧестныйЗнакНоменклатуре(Штрихкоды)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаНоменклатура();
	Запрос.УстановитьПараметр("Штрихкод", Штрихкоды);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	ТекстОшибок = "";
	Пока Выборка.Следующий() Цикл
		НоменклатураОбъект = Выборка.Номенклатура.ПолучитьОбъект();
		НоменклатураОбъект.ЧестныйЗнак = Истина;
		Попытка
			НоменклатураОбъект.Записать();
		Исключение
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Ошибка записи номенклатуры ""%1"".', Код: %2. Описание ошибки: %3"), 
			Выборка.Наименование,
			Выборка.Код,
			ОписаниеОшибки()
			);
		ТекстОшибок = ТекстОшибок + ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ТекстОшибок <> "" Тогда
		ТекстСообщения = НСтр("ru = 'Честный знак!Ошибка записи номенклатуры!'");
		ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибок);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

URL = "xn--80aqu.xn----7sbabas4ajkhfocclk9d3cvfsa.xn--p1ai";
Адрес = "/v4/product-list?limit=1000&offset=0&from_date=2000-01-01%2000:00:01&to_date=2030-01-01%2000:00:01&apikey=ugeh7zyjjrgxeeon";

#КонецОбласти